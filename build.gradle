buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'license'
apply plugin: 'idea'
apply plugin: 'eclipse'

group 'de.johni0702.minecraft'
version '1.0.0'

ext.projectName = 'jGui API'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'
ext.inceptionYear = '2016'

sourceCompatibility = 1.8
targetCompatibility = 1.8

license {
    ext.name = project.projectName
    ext.author = project.author
    ext.url = project.authorUrl
    ext.year = project.inceptionYear
    header new File(project.getProjectDir(), 'HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

apply from: 'versions/preprocessor.gradle'

task setCoreVersion() {
    doLast {
        def vars = [MC: project.mcVersion as int, DEV_ENV: 1]
        project.convertTree(vars, 'src/main/java')
        project.convertTree(vars, 'src/main/resources')
        project.convertTree(vars, 'versions/core/build.gradle')
    }
}

subprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven {
                name = "forge"
                url = "http://files.minecraftforge.net/maven"
            }
            maven {
                name = "sonatype"
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }
}
